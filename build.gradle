buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {url 'https://repo.grails.org/grails/core'}
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
    }
}

// Custom props sourced from gradle.properties
def appCode = xhAppCode,
    appName = xhAppName,
    appVersion = xhAppVersion,
    appBuild = hasProperty('xhAppBuild') ? xhAppBuild : 'UNKNOWN'

version appVersion
group 'io.xh'

apply plugin:'idea'
apply plugin:'war'
apply plugin:'org.grails.grails-web'
apply plugin:'org.grails.grails-gsp'

repositories {
    mavenLocal()
    jcenter()
    maven {url 'https://repo.grails.org/grails/core'}
    maven {url 'https://repo.xh.io/content/groups/public/'}
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

boolean inlineHoist = runHoistInline == 'true'
if (inlineHoist) {
    println "${appName}: running with Hoist Core INLINE...."
} else {
    println "${appName}: running with Hoist Core PACKAGED at v${hoistCoreVersion}...."
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    provided "org.springframework.boot:spring-boot-starter-tomcat"

    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-plugin-i18n"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-interceptors"

    compile "org.grails.plugins:gsp"
    compile "org.grails.plugins:async"
    compile "org.grails.plugins:events"

    compile "org.grails.plugins:hibernate5"
    compile "org.hibernate:hibernate-core:5.1.5.Final"
    compile "org.hibernate:hibernate-ehcache:5.1.5.Final"

    console "org.grails:grails-console"
    profile "org.grails.profiles:web"

    compile "org.springframework:spring-websocket"
    
    runtime "org.apache.tomcat:tomcat-jdbc"
    runtime "mysql:mysql-connector-java:5.1.47"
    runtime "com.h2database:h2"

    if (!inlineHoist) {
        // Include Hoist as versioned external dependency when building a WAR for release
        compile "io.xh:hoist-core:$hoistCoreVersion"
    }
}

if (inlineHoist) {
    // Include Hoist as an inline plugin in multi-project mode when running in development
    grails {
        plugins {
            compile project(":hoist-core")
        }
    }
}

apply plugin:'maven-publish'
publishing {
    publications {
        // Classic Maven publication of the Grails server WAR only (with fully spec'd POM file).
        appWar(MavenPublication) {
            from components.java
            artifactId appCode
            artifact war
        }
        // TAR distro with both the Grails WAR + client-side assets.
        appWarAndClientAssets(MavenPublication) {
            artifactId appCode + '-all'
            artifact appWarAndClientAssetsDistTar
        }
        // TAR distro with client-side assets only.
        clientAssets(MavenPublication) {
            artifactId appCode + '-client-assets'
            artifact clientAssetsDistTar
        }
    }
    repositories {
        maven {
            def repoEndpoint = version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'
            url "https://repo.xh.io/content/repositories/$repoEndpoint/"
            credentials {
                username project.findProperty('xhRepoDeployUser')
                password project.findProperty('xhRepoDeployPassword')
            }
        }
    }
}

// Tasks to create distribution bundles that include built client-assets, as well as (for the
// first task) the WAR. This creates a self-contained archive with both sides of the build output.
// NOTE - these rely on the client-side build having been completed (outside of Gradle) by Yarn and
// Webpack. A future project could be to orchestrate the Webpack build from within Gradle as well.
apply plugin:'distribution'
distributions {
    appWarAndClientAssets {
        contents {
            into ('client-assets') {
                from ('client-app/build')
            }
            into ('war') {
                from war.outputs.files
            }
        }
    }
    clientAssets {
        contents {
            from { 'client-app/build' }
        }
    }
}

bootRun {
    systemProperties = System.properties
    jvmArgs = ["-Xmx1G"]
    addResources = true
}


//--------------------------------------------------
// Extensions to build.info (Hoist-Core requirement)
//--------------------------------------------------
buildProperties.doLast {
    File grailsBuildInfoFile = it.outputs.files.files.find { it.name == 'grails.build.info' }
    if(!grailsBuildInfoFile) return

    Properties properties = new Properties()
    grailsBuildInfoFile.withInputStream {properties.load(it)}
    properties.setProperty('info.xh.appCode', appCode)
    properties.setProperty('info.xh.appName', appName)
    properties.setProperty('info.xh.appBuild', appBuild)
    grailsBuildInfoFile.withOutputStream {properties.store(it, null)}
}


//----------------------------
// Custom Tasks
//----------------------------
task publishAppWar(dependsOn: "publishAppWarPublicationToMavenRepository") {
    group 'xhio'
    description 'Publishes the built Grails server WAR to ExHI Nexus.'

    doLast {
        println "$appCode $version published to repo.xh.io!"
    }
}

task publishAppWarAndClientAssets(dependsOn: ["checkForClientAssets", "publishAppWarAndClientAssetsPublicationToMavenRepository"]) {
    group 'xhio'
    description 'Publishes a combined tarball with WAR and client assets to ExHI Nexus.'

    doLast {
        println "$appCode $version published to repo.xh.io!"
    }
}


task checkForClientAssets() {
    doFirst {
        def clientBuild = new File('client-app/build')
        if (!clientBuild.exists() || !clientBuild.listFiles().size()) {
            throw new RuntimeException('Client build directory not populated - has a webpack build been run (outside of Gradle)?')
        }
    }
}
